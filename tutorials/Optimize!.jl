### A Pluto.jl notebook ###
# v0.19.16

using Markdown
using InteractiveUtils

# โโโก 81249a52-fb98-463a-a6e2-9cf9c25f02fb
using Pkg

# โโโก 727ca485-8350-4adc-9aa0-59bbb84a9205
using Manopt, Manifolds, Random, Colors, PlutoUI

# โโโก 6bf76330-ad0e-11ec-0c00-894872624127
md"""
# Get Started: Optimize!

In this tutorial, we want to use `Manopt.jl` solve the optimization problem

```math
\operatorname*{argmin}_{p โ \mathcal M} f(p)
```

where ``\mathcal M`` is a [Riemannian manifold](https://juliamanifolds.github.io/Manifolds.jl/stable/interface.html#ManifoldsBase.Manifold) and ``f:\mathcal M โ โ`` is the cost function.

We will take a loot at how to define the optimisation problem, that is the manifold, the cost functin and (in our example) a gradient to call one specific solver.

After that we will dive into general keyword arguments that are available for all solvers as well as specific keywords that are available for the solver we consider here.

We will finally consider a nonsmooth example with a second, a little bit more complicated solver.

This tutorial is a [Pluto ๐ notebook](https://github.com/fonsp/Pluto.jl), so if you are reading the `Manopt.jl` documentation you can also [download](https://github.com/JuliaManifolds/Manopt.jl/raw/master/tutorials/Optimize!.jl) the notebook and run it yourself within Pluto.
"""

# โโโก 960f171c-4f52-4104-a827-c6b918b7538d
md"""
## Setup
The following is a little bit of setup to save/include the generated images. If you are running the package locally and have Asymptote installed, you can set `render_asy` to true to generate the images.

If you downloaded only the notebook, the code runs but the images might not show.
"""

# โโโก 4235a1ba-3cf2-49dc-9a26-32fafc7a7008
begin
    localpath = join(splitpath(@__FILE__)[1:(end - 1)], "/") # files folder
    image_prefix = localpath * "/optimize"
    #@info image_prefix
    render_asy = false # on CI or when you do not have asymptote, this should be false
	use_local = false #when developing a new version, keep this false to use the current Julia environment. Set it to `true` to use Pluto's own package management (restart required)
end;

# โโโก 950555e7-80a0-4e1c-bea9-cf3e556f4589
use_local || Pkg.activate()

# โโโก 65489070-9066-46bb-b5b1-52732dbe9bc7
md"""
# Example

To get started with our example we first have to load the necessary packages.
Using the setup from above you can either use a local (Pluto managed) environment or your local environment by setting `use_local` accordingly.

Usually when introducing new features or breaking releases, the Documentation does not use the local variant, otherwise it usually does.
"""

# โโโก 3d010940-aa2e-44d3-988b-5d6b7bec54f0
Pkg.status(["Manopt","Manifolds"])

# โโโก 177cc292-94d3-4344-857e-30483f592a55
md"""
Let's load a few colors from [Paul Tol](https://personal.sron.nl/~pault/).
"""

# โโโก 0b405c42-19a5-480d-b1dc-0fb8811a48fa
begin
    black = RGBA{Float64}(colorant"#000000")
    TolVibrantOrange = RGBA{Float64}(colorant"#EE7733")
    TolVibrantBlue = RGBA{Float64}(colorant"#0077BB")
    TolVibrantTeal = RGBA{Float64}(colorant"#009988")
    TolVibrantMagenta = RGBA{Float64}(colorant"#EE3377")
end;

# โโโก 803fc640-bbed-4700-8a1e-f414c6446eea
md"""
# The data set
"""

# โโโก 278dbd3c-aa91-4d9d-ad49-c3b4b336efe2
md"""
We take a look at a srandom set of points.
"""

# โโโก 7e0301fb-7465-410c-b47c-04686bf44ab1
begin
    n = 100
    ฯ = ฯ / 8
    M = Sphere(2)
    x = 1 / sqrt(2) * [1.0, 0.0, 1.0]
    Random.seed!(42)
    data = [exp(M, x,  ฯ * rand(M; vector_at=x)) for i in 1:n]
end

# โโโก 9b130a57-293d-429d-88b5-78bfacbf836f
asymptote_export_S2_signals(
    image_prefix * "/startDataAndCenter.asy";
    points=[[x], data],
    colors=Dict(:points => [TolVibrantBlue, TolVibrantTeal]),
    dot_size=3.5,
    camera_position=(1.0, 0.5, 0.5),
)

# โโโก d2bc2e63-6ae7-4f54-b176-f74e66365b1a
render_asy && render_asymptote(image_prefix * "/startDataAndCenter.asy"; render=2)

# โโโก 6ea36d50-96f0-46e1-8d90-529f0b23120d
PlutoUI.LocalResource(image_prefix * "/startDataAndCenter.png")

# โโโก e21d6d03-4c61-457b-a9c7-fad5b4f369db
md"""
## Computing the Mean

To compute the mean on the manifold we use the characterization that the
Euclidean mean minimizes the sum of squared distances, and end up with the
following cost function. Its minimizer is called
[Riemannian Center of Mass](https://arxiv.org/abs/1407.2087).

> **Note.**
> There are more sophisticated methods tailored for the specific manifolds available in [Manifolds.jl](https://juliamanifolds.github.io/Manifolds.jl/) see [mean](https://juliamanifolds.github.io/Manifolds.jl/stable/features/statistics.html#Statistics.mean-Tuple{Manifold,AbstractArray{T,1}%20where%20T,AbstractArray{T,1}%20where%20T,ExtrinsicEstimation}).
"""

# โโโก 515eaa1d-8307-45ba-ae63-be070dc2ff1c
F(M, y) = sum(1 / (2 * n) * distance.(Ref(M), Ref(y), data) .^ 2)

# โโโก 00bc7b5b-7cc9-43bc-bf96-b5d2b85ddb9a
gradF(M, y) = sum(1 / n * grad_distance.(Ref(M), data, Ref(y)))

# โโโก 3cc7ce2b-8ae6-4ba9-8298-2050fe7081eb
md"""
Note that the [grad_distance](https://manoptjl.org/stable/functions/gradients.html#Manopt.grad_distance) defaults to the case `p=2`, i.e. the
gradient of the squared distance. For details on convergence of the gradient
descent for this problem, see [^AfsariTronVidal2013].

The easiest way to call the gradient descent is now to call
[gradient_descent](https://manoptjl.org/stable/solvers/gradient_descent.html#Manopt.gradient_descent).
"""

# โโโก c067f4d1-b54b-4228-85cf-70cdbbdc948b
gradient_descent(M, F, gradF, data[1])

# โโโก 7aee0626-14ba-431e-af84-79c2dfc021da
md"""
In order to get more details, we further add the `debug=` keyword argument, which
act as a [decorator pattern](https://en.wikipedia.org/wiki/Decorator_pattern).

The following debug prints

```juliaREPL
# i | Last Change: | F(x): | x:
```
as well as the reason why the algorithm stopped at the end.

The elements passed to `debug=` are postprocessed, there are specifiic symbols and formats we can use. For example `:Iteration` to plot the iteratin, but we can also add a format for the print using `(:Change, "| Last change: %e3.8")`, i.e. the second string is a format for printf.

Note that here we use `PlutoUI` to see the output also here in the notebook
"""

# โโโก 51daafb5-84a0-47fd-ac40-4d53888cd914
with_terminal() do
    global xMean = gradient_descent(
        M,
        F,
        gradF,
        data[1];
        debug=[
            :Iteration,
            (:Change, "change: %1.9f | "),
            (:Cost, " F(x): %1.11f | "),
            "\n",
            :Stop,
        ],
    )
end

# โโโก 863bf8b8-272c-40d6-985f-0a7cf9454756
md"""
While this works fine and finds a point with a very small gradient norm, the default way to determine the stepsize (though using [`ArmijoLinesearch`](https://manoptjl.org/stable/plans/index.html#Manopt.ArmijoLinesearch)) might be a little bit conservative, since for a start we aim for robust defaults.

We can tweak the default values for the `contraction_factor` and the `sufficient_decrease`  of the Armijo linesearch to improve there.
We can further use the `:Stepsize` symbol in the `debug=` specification to also print the obtained step size by Armijo.
We get
"""

# โโโก 38df2fb3-f742-4652-857c-baa403985ff8
with_terminal() do
    global xMean2 = gradient_descent(
        M,
        F,
        gradF,
        data[1];
        stepsize=ArmijoLinesearch(M; contraction_factor=0.999, sufficient_decrease=0.5),
        debug=[
            :Iteration,
            (:Change, "change: %1.9f | "),
            (:Cost, " F(x): %1.11f | "),
            (:Stepsize, " | s: %1.3f"),
            "\n",
            :Stop,
        ],
    )
end

# โโโก 244eb6ea-0bdb-4443-8dc1-40419966198a
md"""
which finishes in 5 steaps, with (numerically) the same cost.
"""

# โโโก eedfedbc-f305-48ff-8aa7-78b6aa6c4d02
F(M, xMean) - F(M, xMean2)

# โโโก 887bd166-c278-48d3-bfb1-31c7026433b1
md"""
Note that other optimization tasks may have other speedup opportunities.

For even more precision, we can further require a smaller gradient norm.
This is done by changing the `StoppingCriterion` used, where several
criteria can be combined using `&` and/or `|`.
For example in the following case, we want to stop if either the gradient norm is _very_ small (`1e-15`) or if we reach 200 iterations, which can be seen as a fallback.
"""

# โโโก a99a5603-6ef5-43e8-a082-54dd20226956
with_terminal() do
    global xMean3 = gradient_descent(
        M,
        F,
        gradF,
        data[1];
	    stepsize=ArmijoLinesearch(M; contraction_factor=0.999, sufficient_decrease=0.5),
        debug=[
            :Iteration,
            (:Change, "change: %1.9f | "),
            (:Cost, " F(x): %1.11f | "),
            (:Stepsize, " | s: %1.3f"),
            "\n",
            :Stop,
        ],
        stopping_criterion=StopWhenGradientNormLess(1e-15) | StopAfterIteration(200),
    )
end

# โโโก 98028747-31dd-4bf8-b4b5-0959d5afb75c
md"""
Let's add this point to our data image
"""

# โโโก fb07943f-54b4-4cb3-b1fd-f3ab06b4d033
asymptote_export_S2_signals(
    image_prefix * "/startDataCenterMean.asy";
    points=[[x], data, [xMean3]],
    colors=Dict(:points => [TolVibrantBlue, TolVibrantTeal, TolVibrantOrange]),
    dot_size=3.5,
    camera_position=(1.0, 0.5, 0.5),
)

# โโโก 5e1e6db2-39da-4857-8745-eda6ae510fa8
render_asy && render_asymptote(image_prefix * "/startDataCenterMean.asy"; render=2)

# โโโก c9e09455-1af2-40aa-aa05-7fa329b5eec7
PlutoUI.LocalResource(image_prefix * "/startDataCenterMean.png")

# โโโก a17ddd91-0839-48fe-ab78-445226ee4ff9
md"""
## Computing the Median

> **Note.**
> There are more sophisticated methods tailored for the specific manifolds available in
> [Manifolds.jl](https://juliamanifolds.github.io/Manifolds.jl/) see [`median`](https://juliamanifolds.github.io/Manifolds.jl/stable/features/statistics.html#Statistics.median-Tuple{Manifold,AbstractArray{T,1}%20where%20T,AbstractArray{T,1}%20where%20T,CyclicProximalPointEstimation}).

Similarly to the mean, you can also define the median as the minimizer of the
distances, see for example [^Baฤรกk2014], but since
this problem is not differentiable, we employ the Cyclic Proximal Point (CPP)
algorithm, described in the same reference. We define
"""

# โโโก 439b700d-dce7-43c4-bb1e-1f263a3f54a4
F2(M, y) = sum(1 / (2 * n) * distance.(Ref(M), Ref(y), data))

# โโโก 79921477-db82-424f-8f8e-9bade1cd63c3
proxes = Function[(M, ฮป, y) -> prox_distance(M, ฮป / n, di, y, 1) for di in data]

# โโโก dc236508-283c-4047-a216-c570e35bc791
md"""
So we call the cyclic proximal point algorithm, this time with a recording, and activate the return of the complete options to access the recorded values. We further increase the display of the cost function to more digits.
"""

# โโโก d2a8250e-7796-454b-a0bf-9970b1b9a2aa
with_terminal() do
    global o = cyclic_proximal_point(
        M,
        F2,
        proxes,
        data[1];
        debug=[
            :Iteration,
            " | ",
            :Change,
            " | ",
            (:Cost, "F(x): %1.12f"),
            " | ",
            :Iterate,
            "\n",
            50,
            :Stop,
        ],
        record=[:Iteration, :Change, :Cost, :Iterate],
        return_state=true,
    )
end

# โโโก 90414c9b-b164-490c-b939-2472d8284887
xMedian = get_solver_result(o)

# โโโก aa88f273-2a01-4d93-857b-8b9c19fcdd1a
md"""
where the differences to `gradient_descent` are as follows

* the third parameter is now an Array of proximal maps
* debug is reduced to only every 50th iteration
* we further activated a `RecordAction` using the `record=` optional
  parameter. These work very similarly to those in debug, but they
  collect their data in an array. The high level interface then returns two
  variables; the `values` do contain an array of recorded
  datum per iteration. Here a tuple containing the iteration, last change and
  cost respectively; see [RedordOptions](https://manoptjl.org/stable/plans/index.html#RecordSolverState-1) for details.

We can access the recorded values using `get_record`, that consists of a tuple per iteration and contains the iteration number, the change and the cost.
"""

# โโโก c835b5ec-085e-4c9d-b777-76036515bcd1
values = get_record(o)

# โโโก 9a19d3bb-4487-4469-a856-b1a0f3f540a7
asymptote_export_S2_signals(
    image_prefix * "/startDataCenterMedianAndMean.asy";
    points=[[x], data, [xMean], [xMedian]],
    colors=Dict(
        :points => [TolVibrantBlue, TolVibrantTeal, TolVibrantOrange, TolVibrantMagenta]
    ),
    dot_size=3.5,
    camera_position=(1.0, 0.5, 0.5),
)

# โโโก e6e8cc14-a67b-4e6a-a83b-73f9ba1e1dcb
render_asy && render_asymptote(image_prefix * "/startDataCenterMedianAndMean.asy"; render=2)

# โโโก 6cd0aaab-e0bf-428e-a84c-f456a32f8e36
md"""
In the following image the mean (orange), median (magenta) are shown.
"""

# โโโก 97e1bec2-3577-423a-a012-ca1b5413b29a
PlutoUI.LocalResource(image_prefix * "/startDataCenterMedianAndMean.png")

# โโโก 0ba01385-6502-4e66-afee-4c4af391c9b9
md"""
## Literature

[^Baฤรกk2014]:
	> Baฤรกk, M: __Computing Medians and Means in Hadamard Spaces.__
    > SIAM Journal on Optimization, Volume 24, Number 3, pp. 1542โ1566,
    > doi: [10.1137/140953393](https://doi.org/10.1137/140953393),
    > arxiv: [1210.2145](https://arxiv.org/abs/1210.2145)
[^AfsariTronVidal2013]:
	> Afsari, B; Tron, R.; Vidal, R.: __On the Convergence of Gradient Descent for Finding the Riemannian Center of Mass__,
	> SIAM Journal on Control and Optimization, Volume 51, Issue 3, pp. 2230โ2260.
	> doi: [10.1137/12086282X](https://doi.org/10.1137/12086282X),
	> arxiv: [1201.0925](https://arxiv.org/abs/1201.0925).
"""

# โโโก Cell order:
# โโ6bf76330-ad0e-11ec-0c00-894872624127
# โโ960f171c-4f52-4104-a827-c6b918b7538d
# โโ4235a1ba-3cf2-49dc-9a26-32fafc7a7008
# โโ65489070-9066-46bb-b5b1-52732dbe9bc7
# โโ81249a52-fb98-463a-a6e2-9cf9c25f02fb
# โโ950555e7-80a0-4e1c-bea9-cf3e556f4589
# โโ727ca485-8350-4adc-9aa0-59bbb84a9205
# โโ3d010940-aa2e-44d3-988b-5d6b7bec54f0
# โโ177cc292-94d3-4344-857e-30483f592a55
# โโ0b405c42-19a5-480d-b1dc-0fb8811a48fa
# โโ803fc640-bbed-4700-8a1e-f414c6446eea
# โโ278dbd3c-aa91-4d9d-ad49-c3b4b336efe2
# โโ7e0301fb-7465-410c-b47c-04686bf44ab1
# โโ9b130a57-293d-429d-88b5-78bfacbf836f
# โโd2bc2e63-6ae7-4f54-b176-f74e66365b1a
# โโ6ea36d50-96f0-46e1-8d90-529f0b23120d
# โโe21d6d03-4c61-457b-a9c7-fad5b4f369db
# โโ515eaa1d-8307-45ba-ae63-be070dc2ff1c
# โโ00bc7b5b-7cc9-43bc-bf96-b5d2b85ddb9a
# โโ3cc7ce2b-8ae6-4ba9-8298-2050fe7081eb
# โโc067f4d1-b54b-4228-85cf-70cdbbdc948b
# โโ7aee0626-14ba-431e-af84-79c2dfc021da
# โโ51daafb5-84a0-47fd-ac40-4d53888cd914
# โโ863bf8b8-272c-40d6-985f-0a7cf9454756
# โโ38df2fb3-f742-4652-857c-baa403985ff8
# โโ244eb6ea-0bdb-4443-8dc1-40419966198a
# โโeedfedbc-f305-48ff-8aa7-78b6aa6c4d02
# โโ887bd166-c278-48d3-bfb1-31c7026433b1
# โโa99a5603-6ef5-43e8-a082-54dd20226956
# โโ98028747-31dd-4bf8-b4b5-0959d5afb75c
# โโfb07943f-54b4-4cb3-b1fd-f3ab06b4d033
# โโ5e1e6db2-39da-4857-8745-eda6ae510fa8
# โโc9e09455-1af2-40aa-aa05-7fa329b5eec7
# โโa17ddd91-0839-48fe-ab78-445226ee4ff9
# โโ439b700d-dce7-43c4-bb1e-1f263a3f54a4
# โโ79921477-db82-424f-8f8e-9bade1cd63c3
# โโdc236508-283c-4047-a216-c570e35bc791
# โโd2a8250e-7796-454b-a0bf-9970b1b9a2aa
# โโ90414c9b-b164-490c-b939-2472d8284887
# โโaa88f273-2a01-4d93-857b-8b9c19fcdd1a
# โโc835b5ec-085e-4c9d-b777-76036515bcd1
# โโ9a19d3bb-4487-4469-a856-b1a0f3f540a7
# โโe6e8cc14-a67b-4e6a-a83b-73f9ba1e1dcb
# โโ6cd0aaab-e0bf-428e-a84c-f456a32f8e36
# โโ97e1bec2-3577-423a-a012-ca1b5413b29a
# โโ0ba01385-6502-4e66-afee-4c4af391c9b9

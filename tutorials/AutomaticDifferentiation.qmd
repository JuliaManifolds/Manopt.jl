---
title: "Using automatic differentiation in Manopt.jl"
---

Since [Manifolds.jl](https://juliamanifolds.github.io/Manifolds.jl/latest/) 0.7, the support of automatic differentiation support has been extended.

This tutorial explains how to use Euclidean tools to derive a gradient for a real-valued function $f:  \mathcal M ‚Üí ‚Ñù$.
Two methods are considered: an intrinsic variant and a variant employing the embedding. These gradients can then be used within any gradient based optimization algorithm in [Manopt.jl](https://manoptjl.org).

While by default [FiniteDifferences.jl](https://juliadiff.org/FiniteDifferences.jl/latest/)are used,
one can also use [FiniteDiff.jl](https://github.com/JuliaDiff/FiniteDiff.jl), [ForwardDiff.jl](https://juliadiff.org/ForwardDiff.jl/stable/), [ReverseDiff.jl](https://juliadiff.org/ReverseDiff.jl/), or [Zygote.jl](https://fluxml.ai/Zygote.jl/).

This tutorial looks at a few possibilities to approximate or derive the gradient of a function $f:\mathcal M ‚Üí ‚Ñù$ on a Riemannian manifold, without computing it yourself. There are mainly two different philosophies:

1. Working _intrinsically_, that is staying on the manifold and in the tangent spaces, considering to approximate the gradient by forward differences.
2. Working in an embedding where all tools from functions on Euclidean spaces can be used, like finite differences or automatic differentiation, and then compute the corresponding Riemannian gradient from there.

```{julia}
#| echo: false
#| code-fold: true
#| output: false
using Pkg;
cd(@__DIR__)
Pkg.activate("."); # for reproducibility use the local tutorial environment.
```

First, load all necessary packages

```{julia}
using Manopt, Manifolds, Random, LinearAlgebra
using FiniteDifferences, ManifoldDiff, ADTypes
Random.seed!(42);
```

## 1. (Intrinsic) forward differences

A first idea is to generalize (multivariate) finite differences to Riemannian manifolds. Let $X_1,\ldots,X_d ‚àà T_p\mathcal M$ denote an orthonormal basis of the tangent space $T_p\mathcal M$ at the point $p‚àà\mathcal M$ on the Riemannian manifold.

The notion of a directional derivative is generalized to a ‚Äúdirection‚Äù $Y‚ààT_p\mathcal M$. Let $c:  [-Œµ,Œµ]$, $Œµ>0$, be a curve with $c(0) = p$, $\dot c(0) = Y$, for example $c(t)= \exp_p(tY)$.
This yields

```math
	Df(p)[Y] = \left. \frac{d}{dt} \right|_{t=0} f(c(t)) = \lim_{t ‚Üí 0} \frac{1}{t}(f(\exp_p(tY))-f(p))
```

The differential $Df(p)[X]$ is approximated by a finite difference scheme for an $h>0$ as

```math
DF(p)[Y] ‚âà G_h(Y) := \frac{1}{h}(f(\exp_p(hY))-f(p))
```

Furthermore the gradient $\operatorname{grad}f$ is the Riesz representer of the differential:

```math
	Df(p)[Y] = g_p(\operatorname{grad}f(p), Y),\qquad \text{ for all } Y ‚àà T_p\mathcal M
```

and since it is a tangent vector, we can write it in terms of a basis as

```math
	\operatorname{grad}f(p) = \sum_{i=1}^{d} g_p(\operatorname{grad}f(p),X_i)X_i
	= \sum_{i=1}^{d} Df(p)[X_i]X_i
```

and perform the approximation from before to obtain

```math
	\operatorname{grad}f(p) ‚âà \sum_{i=1}^{d} G_h(X_i)X_i
```
for some suitable step size $h$. This comes at the cost of $d+1$ function evaluations and $d$ exponential maps.

This is the first variant we can use. An advantage is that it is _intrinsic_ in the sense that it does not require any embedding of the manifold.

### An example: the Rayleigh quotient

The Rayleigh quotient is concerned with finding eigenvalues (and eigenvectors) of a symmetric matrix $A ‚àà ‚Ñù^{(n+1)√ó(n+1)}$. The optimization problem reads

```math
F:  ‚Ñù^{n+1} ‚Üí ‚Ñù,\quad F(\mathbf x) = \frac{\mathbf x^\mathrm{T}A\mathbf x}{\mathbf x^\mathrm{T}\mathbf x}
```

Minimizing this function yields the smallest eigenvalue $\lambda_1$ as a value and the corresponding minimizer $\mathbf x^*$ is a corresponding eigenvector.

Since the length of an eigenvector is irrelevant, there is an ambiguity in the cost function. It can be better phrased on the sphere $ùïä^n$ of unit vectors in $‚Ñù^{n+1}$,

```math
\operatorname*{arg\,min}_{p ‚àà ùïä^n}\ f(p) = \operatorname*{arg\,min}_{\ p ‚àà ùïä^n} p^\mathrm{T}Ap
```

We can compute the Riemannian gradient exactly as

```math
\operatorname{grad} f(p) = 2(Ap - pp^\mathrm{T}Ap)
```

so we can compare it to the approximation by finite differences.

```{julia}
n = 200
A = randn(n + 1, n + 1)
A = Symmetric(A)
M = Sphere(n);

f1(p) = p' * A'p
gradf1(p) = 2 * (A * p - p * p' * A * p)
```

Manifolds provides a finite difference scheme in tangent spaces, that you can introduce to use an existing framework (if the wrapper is implemented) form Euclidean space. Here we use `FiniteDiff.jl`.

```{julia}
r_backend = ManifoldDiff.TangentDiffBackend(
    AutoFiniteDifferences(central_fdm(5, 1))
)
gradf1_FD(p) = ManifoldDiff.gradient(M, f1, p, r_backend)

p = zeros(n + 1)
p[1] = 1.0
X1 = gradf1(p)
X2 = gradf1_FD(p)
norm(M, p, X1 - X2)
```

We obtain quite a good approximation of the gradient.

``## [2. Conversion of a Euclidean gradient in the embedding to a Riemannian Gradient of a (not Necessarily Isometrically) embedded manifold](@id EmbeddedGradient)``{=commonmark}

Let $\tilde f: ‚Ñù^m ‚Üí ‚Ñù$ be a function on the embedding of an $n$-dimensional manifold $\mathcal M \subset ‚Ñù^m$and let $f:  \mathcal M ‚Üí ‚Ñù$ denote the restriction of $\tilde f$ to the manifold $\mathcal M$.

Since we can use the pushforward of the embedding to also embed the tangent space $T_p\mathcal M$, $p‚àà\mathcal M$, we can similarly obtain the differential $Df(p):  T_p\mathcal M ‚Üí ‚Ñù$ by restricting the differential $D\tilde f(p)$ to the tangent space.

If both $T_p\mathcal M$ and $T_p‚Ñù^m$ have the same inner product, or in other words the manifold is isometrically embedded in $‚Ñù^m$ (like for example the sphere $\mathbb S^n\subset‚Ñù^{m+1}$), then this restriction of the differential directly translates to a projection of the gradient

```math
\operatorname{grad}f(p) = \operatorname{Proj}_{T_p\mathcal M}(\operatorname{grad} \tilde f(p))
```

More generally take a change of the metric into account as

```math
\langle  \operatorname{Proj}_{T_p\mathcal M}(\operatorname{grad} \tilde f(p)), X \rangle
= Df(p)[X] = g_p(\operatorname{grad}f(p), X)
```

or in words: we have to change the Riesz representer of the (restricted/projected) differential of $f$ ($\tilde f$) to the one with respect to the Riemannian metric. This is done using ``[`change_representer`](@extref `ManifoldsBase.change_representer-Tuple{AbstractManifold, AbstractMetric, Any, Any}`)``{=commonmark}.

### A continued example

We continue with the Rayleigh Quotient from before, now just starting with the definition of the Euclidean case in the embedding, the function $F$.

```{julia}
F(x) = x' * A * x / (x' * x);
```

The cost function is the same by restriction

```{julia}
f2(M, p) = F(p);
```

The gradient is now computed combining our gradient scheme with FiniteDifferences.

```{julia}
function grad_f2_AD(M, p)
    b = Manifolds.RiemannianProjectionBackend(AutoFiniteDifferences(central_fdm(5, 1)))
    return Manifolds.gradient(M, F, p, b)
end
X3 = grad_f2_AD(M, p)
norm(M, p, X1 - X3)
```

### An example for a non-isometrically embedded manifold

on the manifold $\mathcal P(3)$ of symmetric positive definite matrices.

The following function computes (half) the distance squared (with respect to the linear affine metric) on the manifold $\mathcal P(3)$ to the identity matrix $I_3$. Denoting the unit matrix we consider the function

```math
    G(q)
    = \frac{1}{2}d^2_{\mathcal P(3)}(q,I_3)
    = \lVert \operatorname{Log}(q) \rVert_F^2,
```

where $\operatorname{Log}$ denotes the matrix logarithm and $\lVert \cdot \rVert_F$ is the Frobenius norm.
This can be computed for symmetric positive definite matrices by summing the squares of the logarithms of the eigenvalues of $q$ and dividing by two:

```{julia}
G(q) = sum(log.(eigvals(Symmetric(q))) .^ 2) / 2
```

We can also interpret this as a function on the space of matrices and apply the Euclidean finite differences machinery; in this way we can easily derive the Euclidean gradient. But when computing the Riemannian gradient, we have to change the representer (see again ``[`change_representer`](@extref `ManifoldsBase.change_representer-Tuple{AbstractManifold, AbstractMetric, Any, Any}`)``{=commonmark}) after projecting onto the tangent space $T_p\mathcal P(n)$ at $p$.

Let's first define a point and the manifold $N=\mathcal P(3)$.

```{julia}
rotM(Œ±) = [1.0 0.0 0.0; 0.0 cos(Œ±) sin(Œ±); 0.0 -sin(Œ±) cos(Œ±)]
q = rotM(œÄ / 6) * [1.0 0.0 0.0; 0.0 2.0 0.0; 0.0 0.0 3.0] * transpose(rotM(œÄ / 6))
N = SymmetricPositiveDefinite(3)
is_point(N, q)
```

We could first just compute the gradient using `FiniteDifferences.jl`, but this yields the Euclidean gradient:

```{julia}
FiniteDifferences.grad(central_fdm(5, 1), G, q)
```

Instead, we use the ``[`RiemannianProjectedBackend`](@extref `ManifoldDiff.RiemannianProjectionBackend`)``{=commonmark} of ``[`ManifoldDiff.jl`](@extref ManifoldDiff :std:doc:`index`)``{=commonmark}, which in this case internally uses `FiniteDifferences.jl` to compute a Euclidean gradient but then uses the conversion explained before to derive the Riemannian gradient.

We define this here again as a function `grad_G_FD` that could be used in the `Manopt.jl` framework within a gradient based optimization.

```{julia}
function grad_G_FD(N, q)
    return Manifolds.gradient(
        N,
        G,
        q,
        ManifoldDiff.RiemannianProjectionBackend(AutoFiniteDifferences(central_fdm(5, 1))),
    )
end
G1 = grad_G_FD(N, q)
```

Now, we can again compare this to the (known) solution of the gradient, namely the gradient of (half of) the distance squared $G(q) = \frac{1}{2}d^2_{\mathcal P(3)}(q,I_3)$ is given by $\operatorname{grad} G(q) = -\operatorname{log}_q I_3$, where $\operatorname{log}$ is th ``[logarithmic map](@extref Manifolds :jl:method:`Base.log-Tuple{SymmetricPositiveDefinite, Vararg{Any}}`)``{=commonmark} on the manifold.

```{julia}
G2 = -log(N, q, Matrix{Float64}(I, 3, 3))
```

Both terms agree up to $1.8√ó10^{-12}$:

```{julia}
norm(G1 - G2)
isapprox(M, q, G1, G2; atol=2 * 1e-12)
```

## Summary

This tutorial illustrates how to use tools from Euclidean spaces, finite differences or automatic differentiation, to compute gradients on Riemannian manifolds. The scheme allows to use _any_ differentiation framework within the embedding to derive a Riemannian gradient.

## Technical details

This tutorial is cached. It was last run on the following package versions.

```{julia}
#| code-fold: true
#| echo: false
using Pkg
Pkg.status()
```
```{julia}
#| code-fold: true
#| echo: false
#| output: asis
using Dates
println("This tutorial was last rendered $(Dates.format(now(), "U d, Y, HH:MM:SS")).");
```
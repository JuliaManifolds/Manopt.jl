---
title: "How to record data during the iterations"
author: "Ronny Bergmann"
---

The recording and debugging features make it possible to record nearly any data during the iterations.
This tutorial illustrates how to:

* record one value during the iterations;
* record multiple values during the iterations and access them afterwards;
* record within a subsolver
* define an own `RecordAction` to perform individual recordings.

Several predefined recordings exist, for example [`RecordCost`](@ref) or [`RecordGradient`](https://manoptjl.org/stable/solvers/gradient_descent/#Manopt.RecordGradient), if the problem the solver uses provides a gradient.
For fields of the `State` the recording can also be done [`RecordEntry`](@ref).
For other recordings, for example more advanced computations before storing a value, an own `RecordAction` can be defined.

We illustrate these using the gradient descent from the [Get started: optimize](getstarted.md) tutorial.

Here the focus is put on ways to investigate the behaviour during iterations by using Recording techniques.

```{julia}
#| echo: false
#| code-fold: true
#| output: false
using Pkg;
cd(@__DIR__)
Pkg.activate("."); # for reproducibility use the local tutorial environment.
```

Let's first load the necessary packages.

```{julia}
using Manopt, Manifolds, Random, ManifoldDiff, LinearAlgebra
using ManifoldDiff: grad_distance
Random.seed!(42);
```

## The objective

We generate data and define our cost and gradient:

```{julia}
Random.seed!(42)
m = 30
M = Sphere(m)
n = 800
σ = π / 8
x = zeros(Float64, m + 1)
x[2] = 1.0
data = [exp(M, x, σ * rand(M; vector_at=x)) for i in 1:n]
f(M, p) = sum(1 / (2 * n) * distance.(Ref(M), Ref(p), data) .^ 2)
grad_f(M, p) = sum(1 / n * grad_distance.(Ref(M), data, Ref(p)))
```

## First examples

For the high level interfaces of the solvers, like [`gradient_descent`](https://manoptjl.org/stable/solvers/gradient_descent.html) we have to set `return_state` to `true` to obtain the whole [solver state](https://manoptjl.org/stable/plans/state/) and not only the resulting minimizer.

Then we can easily use the `record=` option to add recorded values. This keyword accepts [`RecordAction`](https://manoptjl.org/stable/plans/record/#Manopt.RecordAction)s
as well as several symbols as shortcuts, for example `:Cost` to record the cost, or if your options have a field `f`, `:f` would record that entry.
An overview of the symbols that can be used is given [here](https://manoptjl.org/stable/plans/record/#Manopt.RecordActionFactory-Tuple{AbstractManoptSolverState,%20RecordAction}).

We first just record the cost after every iteration

```{julia}
R = gradient_descent(M, f, grad_f, data[1]; record=:Cost, return_state=true)
```

From the returned state, we see that the [`GradientDescentState`](https://manoptjl.org/stable/solvers/gradient_descent/#Manopt.GradientDescentState) are encapsulated (decorated) within a [`RecordSolverState`](https://manoptjl.org/stable/plans/record/#Manopt.RecordSolverState).

For such a state, one can attach different recorders to some operations, currently to `:Start`. `:Stop`, and `:Iteration`, where `:Iteration` is the default when using the `record=` keyword with a [`RecordAction`](https://manoptjl.org/stable/plans/record/#Manopt.RecordAction) or a `Symbol` as we just did.
We can access all values recorded during the iterations by calling `get_record(R, :Iteation)` or since this is the default even shorter

```{julia}
get_record(R)
```

To record more than one value, you can pass an array of a mix of symbols and [`RecordAction`](https://manoptjl.org/stable/plans/record/#Manopt.RecordAction)s which formally introduces [`RecordGroup`](https://manoptjl.org/stable/plans/record/#Manopt.RecordGroup). Such a group records a tuple of values in every iteration:

```{julia}
R2 = gradient_descent(M, f, grad_f, data[1]; record=[:Iteration, :Cost], return_state=true)
```

Here, the symbol `:Cost` is mapped to using the [`RecordCost`](https://manoptjl.org/stable/plans/record/#Manopt.RecordCost) action. The same holds for `:Iteration` obviously records the current iteration number `i`.
To access these you can first extract the group of records (that is where the `:Iteration`s are recorded; note the plural) and then access the `:Cost`
"""

```{julia}
get_record_action(R2, :Iteration)
```

Since `iteration` is the default, we can also omit it here again.
To access single recorded values, one can use

```{julia}
get_record_action(R2)[:Cost]
```

This can be also done by using a the high level interface [`get_record`](https://manoptjl.org/stable/plans/record/#Manopt.get_record)

```{julia}
get_record(R2, :Iteration, :Cost)
```

Note that the first symbol again refers to the point where we record (not to the thing we record).
We can also pass a tuple as second argument to have our own order within the tuples returned. Switching the order of recorded cost and Iteration can be done using
"""

```{julia}
get_record(R2, :Iteration, (:Iteration, :Cost))
```

## A more complex example

To illustrate a complicated example let's record:

* the iteration number, cost and gradient field, but only every sixth iteration;
* the iteration at which we stop.

We first generate the problem and the state, to also illustrate the low-level works when not using the high-level interface [`gradient_descent`](https://manoptjl.org/stable/solvers/gradient_descent.html).

```{julia}
p = DefaultManoptProblem(M, ManifoldGradientObjective(f, grad_f))
s = GradientDescentState(
    M;
    p=copy(data[1]),
    stopping_criterion=StopAfterIteration(200) | StopWhenGradientNormLess(10.0^-9),
)
```

We now first build a  [`RecordGroup`](https://manoptjl.org/stable/plans/record/#Manopt.RecordGroup) to group the three entries we want to record per iteration. We then put this into a [`RecordEvery`](https://manoptjl.org/stable/plans/record/#Manopt.RecordEvery) to only record this every sixth iteration

```{julia}
rI = RecordEvery(
    RecordGroup([
        RecordIteration() => :Iteration,
        RecordCost() => :Cost,
        RecordEntry(similar(data[1]), :X) => :Gradient,
    ]),
    6,
)
```

where the notation as a pair with the symbol can be read as “Is accessible by”.
The `record=` keyword with the symbol `:Iteration` is actually the same as we specified here for the first group entry.
For recording the final iteration number

```{julia}
sI = RecordIteration()
```

We now combine both into the [`RecordSolverState`](https://manoptjl.org/stable/plans/record/#Manopt.RecordSolverState) decorator. It acts completely the same as any [`AbstractManoptSolverState`](https://manoptjl.org/stable/plans/state/#Manopt.AbstractManoptSolverState) but records something in every iteration additionally. This is stored in a dictionary of [`RecordAction`](https://manoptjl.org/stable/plans/record/#Manopt.RecordAction)s, where `:Iteration` is the action (here the only every sixth iteration group) and the `sI` which is executed at stop.

Note that the keyword `record=` in the high level interface `gradient_descent` only would fill the `:Iteration` symbol of said dictionary, but we could also pass pairs like in the following,
that is in the form `Symbol => RecordAction` into that keyword to obtain the same as in

```{julia}
r = RecordSolverState(s, Dict(:Iteration => rI, :Stop => sI))
```

We now call the solver

```{julia}
res = solve!(p, r)
```

And we can look at the recorded value at `:Stop` to see how many iterations were performed

```{julia}
get_record(res, :Stop)
```

and the other values during the iterations are

```{julia}
get_record(res, :Iteration, (:Iteration, :Cost))
```

where the last tuple contains the names from the pairs when we generated the record group.
So similarly we can use `:Gradient` as specified before to access the recorded gradient.

## Recording from a Subsolver

One can also record from a subsolver. For that we need a problem that actually requires a subsolver. We take the constraint example from the
[How to print debug](HowToDebug.md) tutorial.
Maybe read that part for more details on the problem


```{julia}
#| output: false
d = 4
M2 = Sphere(d - 1)
v0 = project(M2, [ones(2)..., zeros(d - 2)...])
Z = v0 * v0'
#Cost and gradient
f2(M, p) = -tr(transpose(p) * Z * p) / 2
grad_f2(M, p) = project(M, p, -transpose.(Z) * p / 2 - Z * p / 2)
# Constraints
g(M, p) = -p # now p ≥ 0
mI = -Matrix{Float64}(I, d, d)
# Vector of gradients of the constraint components
grad_g(M, p) = [project(M, p, mI[:, i]) for i in 1:d]
p0 = project(M2, [ones(2)..., zeros(d - 3)..., 0.1])
```

We directly start with recording the sub solvers Iteration.
We can specify what to record in the subsolver using the `sub_kwargs`
keyword argument with a `Symbol => value` pair. Here we specify to record the iteration and the cost in every sub solvers step.

Furthermore, we have to “collect” this recording after every sub solver run.
This is done with the `:Subsolver` keyword in the main `record=` keyword.


```{julia}
#| output: false
s1 = exact_penalty_method(
    M2,
    f2,
    grad_f2,
    p0;
    g = g,
    grad_g = grad_g,
    record = [:Iteration, :Cost, :Subsolver],
    sub_kwargs = [:record => [:Iteration, :Cost]],
    return_state=true,
);
```

Then the first entry of the record contains the iterate, the (main solvers) cost, and the third entry is the recording of the subsolver.

```{julia}
get_record(s1)[1]
```

When adding a number to not record on every iteration, the `:Subsolver` keyword of course still also only “copies over” the subsolver recordings when active. But one could avoid allocations on the other runs. This is done, by specifying the sub solver as `:WhenActive`

```{julia}
#| output: false
s2 = exact_penalty_method(
    M2,
    f2,
    grad_f2,
    p0;
    g = g,
    grad_g = grad_g,
    record = [:Iteration, :Cost, :Subsolver, 25],
    sub_kwargs = [:record => [:Iteration, :Cost, :WhenActive]],
    return_state=true,
);
```

Then

```{julia}
get_record(s2)
```

Finally, instead of recording iterations, we can also specify to record the stopping criterion and final cost by adding that to `:Stop` of the sub solvers record. Then we can specify, as usual in a tuple, that the `:Subsolver` should record `:Stop` (by default it takes over `:Iteration`)

```{julia}
#| output: false
s3 = exact_penalty_method(
    M2,
    f2,
    grad_f2,
    p0;
    g = g,
    grad_g = grad_g,
    record = [:Iteration, :Cost, (:Subsolver, :Stop), 25],
    sub_kwargs = [:record => [:Stop => [:Stop, :Cost]]],
    return_state=true,
);
```

Then the following displays also the reasons why each of the recorded sub solvers stopped and the corresponding cost

```{julia}
get_record(s3)
```

## Writing an own [`RecordAction`](https://manoptjl.org/stable/plans/record/#Manopt.RecordAction)s

Let's investigate where we want to count the number of function evaluations, again just to illustrate, since for the gradient this is just one evaluation per iteration.
We first define a cost, that counts its own calls.

```{julia}
mutable struct MyCost{T}
    data::T
    count::Int
end
MyCost(data::T) where {T} = MyCost{T}(data, 0)
function (c::MyCost)(M, x)
    c.count += 1
    return sum(1 / (2 * length(c.data)) * distance.(Ref(M), Ref(x), c.data) .^ 2)
end
```

and we define an own, new [`RecordAction`](https://manoptjl.org/stable/plans/record/#Manopt.RecordAction), which is a functor, that is a struct that is also a function. The function we have to implement is similar to a single solver step in signature, since it might get called every iteration:

```{julia}
mutable struct RecordCount <: RecordAction
    recorded_values::Vector{Int}
    RecordCount() = new(Vector{Int}())
end
function (r::RecordCount)(p::AbstractManoptProblem, ::AbstractManoptSolverState, i)
    if i > 0
        push!(r.recorded_values, Manopt.get_cost_function(get_objective(p)).count)
    elseif i < 0 # reset if negative
        r.recorded_values = Vector{Int}()
    end
end
```

Now we can initialize the new cost and call the gradient descent.
Note that this illustrates also the last use case since you can pass symbol-action pairs into the `record=`array.

```{julia}
#| output: false
f3 = MyCost(data)
```

Now for the plain gradient descent, we have to modify the step (to a constant stepsize) and remove the default debug verification whether the cost increases (setting `debug` to `[]`).
We also only look at the first 20 iterations to keep this example small in recorded values. We call

```{julia}
R3 = gradient_descent(
    M,
    f3,
    grad_f,
    data[1];
    record=[:Iteration => [
        :Iteration,
        RecordCount() => :Count,
        :Cost],
    ],
    stepsize = ConstantLength(1.0),
    stopping_criterion=StopAfterIteration(20),
    debug=[],
    return_state=true,
)
```

For `:Cost` we already learned how to access them, the ` => :Count` introduces an action to obtain the `:Count` symbol as its access. We can again access the whole sets of records

```{julia}
get_record(R3)
```

this is equivalent to calling `R[:Iteration]`.
Note that since we introduced `:Count` we can also access a single recorded value using

```{julia}
R3[:Iteration, :Count]
```

and we see that the cost function is called once per iteration.

If we use this counting cost and run the default gradient descent with Armijo line search, we can infer how many Armijo line search backtracks are preformed:

```{julia}
f4 = MyCost(data)
```

To not get too many entries let's just look at the first 20 iterations again

```{julia}
R4 = gradient_descent(
    M,
    f4,
    grad_f,
    data[1];
    record=[RecordCount(),],
    return_state=true,
)
```

```{julia}
get_record(R4)
```

We can see that the number of cost function calls varies, depending on how many line search backtrack steps were required to obtain a good stepsize.

## Technical details

This tutorial is cached. It was last run on the following package versions.

```{julia}
#| code-fold: true
#| echo: false
using Pkg
Pkg.status()
```
```{julia}
#| code-fold: true
#| echo: false
#| output: asis
using Dates
println("This tutorial was last rendered $(Dates.format(now(), "U d, Y, HH:MM:SS")).");
```